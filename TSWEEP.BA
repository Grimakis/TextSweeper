1 ' VERSION 2.1
2 ' COPYRIGHT, GEORGE M. RIMAKIS, 2023.
3 ' ALL RIGHTS RESERVED.
900 GOTO 14000
2915 PRINT @ 192," SQUARES";:PRINT @ 232,"   LEFT:";
2916 PRINT @ 272,"        ";:PRINT @ 272,"    "+STR$(MC%);
3000 PRINTCN$;:PRINT@Y*40+X,;
3020 ON INSTR(1,S$,INKEY$) GOTO 3200,3500,3520,3540,3560,3600,3600,3700,3700
3200 IF MC%=0 THEN WL$="    WIN!":GOTO 12210
3490 GOTO 3000
3500 IF X<31 THEN X=X+1
3510 GOTO 3000
3520 IF X>0 THEN X=X-1
3530 GOTO 3000
3540 IF Y<7 THEN Y=Y+1
3550 GOTO 3000
3560 IF Y>0 THEN Y=Y-1
3570 GOTO 3000
3600 IF S%(X,Y,1)=239 THEN S%(X,Y,1)=176:PRINTCHR$(S%(X,Y,1));:GOTO 3000
3610 IF S%(X,Y,1)=176 THEN S%(X,Y,1)=239:PRINTCHR$(S%(X,Y,1));:GOTO 3000
3620 GOTO 3000
3700 PRINTCF$;
4000 IF M=1 THEN GOTO 5000
4002 PRINT @ 129,"CALCULATING...";
4010 N=0 : REM minefield generation starts here
4020 SEC = -60*VAL(MID$(TIME$,4,2))+VAL(RIGHT$(TIME$,2))
4030 DUMMY= RND(SE)
4040 DUMMY= RND(1)
4060 IF N>=MN% THEN GOTO 4200
4070 DX= FIX(RND(1)*32)
4080 DY= FIX(RND(1)*8)
4100 IF DX<>X-1 AND DX<>X AND DX<>X+1 THEN GOTO 4110
4105 IF DY=Y-1 OR DY=Y OR DY=Y+1 THEN GOTO 4070
4110 IF S%(DX,DY,0)=0 THEN S%(DX,DY,0)=1 ELSE GOTO 4040
4120 N = N+1:GOTO 4040
4200 M=1 : REM minefield generation complete
4300 FOR Q=0 TO 13
4400 PRINT @ 129+Q,CHR$(239)
4450 NEXT Q
4500 GOTO 5000
5000 IF S%(X,Y,0)=1 THEN GOTO 10000 : REM reveal board if we hit a mine
5010 IF S%(X,Y,0)=2 THEN GOTO 3000 : REM go back to mail loop if cleared
5020 S%(X,Y,0)=2:MC%=MC%-1 
5050 ST%(W%,0)=X:ST%(W%,1)=Y:W%=W%+1
5060 CX=ST%(R%,0):CY=ST%(R%,1):R%=R%+1
6010 C=0
7000 IF CY-1<0 THEN GOTO 7060
7010 IF CX-1<0 THEN GOTO 7030
7020 IF S%(CX-1,CY-1,0)=1 THEN C=C+1:REM Check Top Left
7030 IF S%(CX,CY-1,0)=1 THEN C=C+1:REM Check Top Middle
7040 IF CX+1>31 THEN GOTO 7060
7050 IF S%(CX+1,CY-1,0)=1 THEN C=C+1:REM Check Top Right
7060 IF CX-1<0 THEN GOTO 7080
7070 IF S%(CX-1,CY,0)=1 THEN C=C+1:REM Check Middle Left
7080 IF CX+1>31 THEN GOTO 7100
7090 IF S%(CX+1,CY,0)=1 THEN C=C+1:REM Check Middle Right
7100 IF CY+1>7 THEN GOTO 7160
7110 IF CX-1<0 THEN GOTO 7130
7120 IF S%(CX-1,CY+1,0)=1 THEN C=C+1:REM Check Bottom Left
7130 IF S%(CX,CY+1,0)=1 THEN C=C+1:REM Check Bottom Middle
7140 IF CX+1>31 THEN GOTO 7160
7150 IF S%(CX+1,CY+1,0)=1 THEN C=C+1:REM Check Bottom Right
7160 IF C=0 THEN PRINT @ (CY*40)+CX,CHR$(32);:S%(CX,CY,1)=32
7170 IF C>0 THEN PRINT @ (CY*40)+CX,RIGHT$(STR$(C),1);:S%(CX,CY,1)=ASC(RIGHT$(STR$(C),1))
7200 IF C=0 THEN GOTO 8000
7300 IF R%=W% THEN GOTO 2915
7400 IF R%<W% THEN GOTO 5060
8000 IF CY-1<0 THEN GOTO 8060
8010 IF CX-1<0 THEN GOTO 8030
8020 IF S%(CX-1,CY-1,0)=0 THEN ST%(W%,0)=CX-1:ST%(W%,1)=CY-1:W%=W%+1:S%(CX-1,CY-1,0)=2:MC%=MC%-1
8030 IF S%(CX,CY-1,0)=0 THEN ST%(W%,0)=CX:ST%(W%,1)=CY-1:W%=W%+1:S%(CX,CY-1,0)=2:MC%=MC%-1
8040 IF CX+1>31 THEN GOTO 8060
8050 IF S%(CX+1,CY-1,0)=0 THEN ST%(W%,0)=CX+1:ST%(W%,1)=CY-1:W%=W%+1:S%(CX+1,CY-1,0)=2:MC%=MC%-1
8060 IF CX-1<0 THEN GOTO 8080
8070 IF S%(CX-1,CY,0)=0 THEN ST%(W%,0)=CX-1:ST%(W%,1)=CY:W%=W%+1:S%(CX-1,CY,0)=2:MC%=MC%-1
8080 IF CX+1>31 THEN GOTO 8100
8090 IF S%(CX+1,CY,0)=0 THEN ST%(W%,0)=CX+1:ST%(W%,1)=CY:W%=W%+1:S%(CX+1,CY,0)=2:MC%=MC%-1
8100 IF CY+1>7 THEN GOTO 8160
8110 IF CX-1<0 THEN GOTO 8130
8120 IF S%(CX-1,CY+1,0)=0 THEN ST%(W%,0)=CX-1:ST%(W%,1)=CY+1:W%=W%+1:S%(CX-1,CY+1,0)=2:MC%=MC%-1
8130 IF S%(CX,CY+1,0)=0 THEN ST%(W%,0)=CX:ST%(W%,1)=CY+1:W%=W%+1:S%(CX,CY+1,0)=2:MC%=MC%-1
8140 IF CX+1>31 THEN GOTO 8160
8150 IF S%(CX+1,CY+1,0)=0 THEN ST%(W%,0)=CX+1:ST%(W%,1)=CY+1:W%=W%+1:S%(CX+1,CY+1,0)=2:MC%=MC%-1
8160 IF R%<W% GOTO 5060 ELSE GOTO 2915
10000 Y=0: X=0
12000 FOR X = 0 TO 31
12010 N = X+Y*40
12110 IF S%(X,Y,0)=1 THEN PRINT @ N,CHR$(42);
12120 NEXT X
12130 Y = Y + 1
12150 IF Y<=7 GOTO 12000
12210 PRINT @ 32,"     YOU";
12220 PRINT @ 72,        WL$;
12230 PRINT @ 112,"        ";
12240 PRINT @ 192,"    Play";
12250 PRINT @ 232,"  Again?";
12260 PRINT @ 272,"   (Y/N)";
12262 PRINT @ 312,"       ";
12270 A$=INKEY$
12280 IF A$="" THEN A$="Q"
12290 IF A$="Y" OR A$="y" THEN GOTO 14100
12300 IF A$="N" OR A$="n" THEN END
12360 GOTO 12270
14000 SCREEN 0,0
14010 CLS
14020 M=0:Q=0:N=1:DUMMY=0:SEC=0
14030 PRINT @ 94,"TEXT SWEEPER"
14040 PRINT @ 133,"BY G.M. RIMAKIS"
14050 PRINT @ 207,"Press [SPACEBAR] to start."
14060 S$="Q"+CHR$(32)
14070 ON INSTR(1,S$,INKEY$) GOTO 14070,14100
14080 GOTO 14070
14100 CLS:CLEAR:S$="Q"+CHR$(28)+CHR$(29)+CHR$(31)+CHR$(30)+CHR$(70)+CHR$(102)+CHR$(32)+CHR$(13):WL$="   LOSE!":CN$=CHR$(27)+CHR$(80):CF$=CHR$(27)+CHR$(81)
14110 PRINT"Use default settings?(Y/N)"
14120 A$=INKEY$
14130 IF A$="" THEN A$="Q"
14140 IF A$="Y" OR A$="y" THEN GOTO 14200
14160 IF A$="N" OR A$="n" THEN GOTO 14250
14180 GOTO 14120
14200 MN%=36:GOTO 15030
14250 CLS:INPUT"Enter number of mines. (10-217)";MN%
14300 IF MN%<10 OR MN%>217 GOTO 14250
15030 PRINT @ 32,"    TEXT";
15040 PRINT @ 72," SWEEPER";
15090 LET X1%=0:Y1%=0:R%=0:W%=0
15100 M=0:MC%=256-MN%
16060 LET X=0:Y=0
16070 DIM S%(31,7,1)
16075 DIM ST%(271,1)
16080 FOR X = 0 TO 31
16090 S%(X,Y,0)=0
16095 S%(X,Y,1)=239
16110 PRINT @ X+Y*40,CHR$(239);
16150 NEXT X
16160 Y = Y+1
16170 IF Y <=7 GOTO 16080
16180 X=16:Y=3
16200 M=0
16500 GOTO 2915