1 CLEAR:VE$="2.5.0":CLS
2 ' COPYRIGHT, GEORGE M. RIMAKIS, 2017-2023.
3 ' ALL RIGHTS RESERVED.
10 DATA 30,00,70,35,78,35,120,22,1,146,139,119,35,120,22,225,130,120,60,155,119,35,121,22,1,146,139,119,35,121,22,249,130,121,60,155,119,201 : 'ASM - Calculates XY Boundaries
11 DIM XY%(21):LET A%=0:FOR N=0 TO 37:READ A%: POKE VARPTR(XY%(3))+N, A%: NEXT N
20 DATA 1,0,0,17,8,32,112,35,113,35,4,120,186,218,6,8,6,0,12,121,187,218,6,8,201 : 'ASM - Generates initial mine pool
21 DIM MA%(255): DIM MG%(12):FOR N=0 TO 24:READ A%: POKE VARPTR(MG%(0))+N, A%: NEXT N
30 DATA 0,17,0,32,213,30,255,71,87,62,1,78,35,35,129,122,206,0,87,12,121,198,255,123,206,0,95,184,122,194,45,8,225,114,201 : 'ASM - Adjusts mine pool index for already picked tiles
31 DIM TS%(18):FOR N=0 TO 34:READ A%: POKE VARPTR(TS%(0))+N, A%: NEXT N
100 IF PEEK(1)<>171 AND PEEK(63035)=8 THEN LD%=0 : H$="  [H]elp" : GOTO 900 
110 IF (PEEK(1)<>171 AND PEEK(63035)=25) OR (PEEK(1)=171 AND PEEK(61190)=25) THEN PO%=640 ELSE PO%=320
120 LD% = 1 : H$="        "
900 GOSUB 24000:GOTO 14000
2915 GOSUB 17300 ' print square count
3000 FOR N = 0 TO 50
3005 KEY(8) ON: IF N=0 THEN PRINT@Y*40+X,CN$; ELSE IF N=25 THEN PRINT@Y*40+X,CF$;ELSE PRINT"";
3010 IF PEEK(65339)<>72 AND LD%<>1 THEN PRINT CHR$(27)+"U":GOSUB 17100
3020 ON INSTR(1,S$,INKEY$) GOTO 3030,3500,3520,3540,3560,3600,3600,4000,4000,3560,3560,3520,3520,3540,3540,3500,3500,21000,21000
3030 NEXT N : GOTO 3000
3200 IF MC%=0 THEN ME$(1)="    WIN!":GOTO 12200 ELSE GOTO 2915
3500 IF X<31 THEN X=X+1 : GOTO 3000 ELSE : GOTO 3000
3520 IF X>0 THEN X=X-1 : GOTO 3000 ELSE : GOTO 3000
3540 IF Y<7 THEN Y=Y+1 : GOTO 3000 ELSE : GOTO 3000
3560 IF Y>0 THEN Y=Y-1 : GOTO 3000 ELSE : GOTO 3000
3600 IF S%(X,Y,1)=0 THEN S%(X,Y,1)=1:PRINTCHR$(255);:GOTO 3000
3610 IF S%(X,Y,1)=1 THEN S%(X,Y,1)=0:PRINTCHR$(239);:GOTO 3000
3620 GOTO 3000
4000 GOSUB 24000:IF M=1 THEN GOTO 5000
4001 IF S%(X,Y,1)=1 THEN GOTO 3000
4002 PRINT @ 129,"PLACING MINES!";
4010 N=0 'minefield generation starts here
4013 MG%(7)=VARPTR(MG%(3)):MG%(11)=VARPTR(MG%(3)):CALL VARPTR(MG%(0)),0,VARPTR(MA%(0))
4015 POKE VARPTR(XY%(0)), X: POKE VARPTR(XY%(0))+1, Y : CALL VARPTR(XY%(3)),0,VARPTR(XY%(0)) 
4016 J%=0:FOR TY% = PEEK(VARPTR(XY%(2))) TO PEEK(VARPTR(XY%(2))+1):FOR TX% = PEEK(VARPTR(XY%(1))) TO PEEK(VARPTR(XY%(1))+1)
4017 MA%(32*TY%+TX%)=-1: J%=J%+1
4018 NEXT:NEXT
4020 SEC = (-3600*VAL(RIGHT$(TIME$,2)))+(-60*VAL(MID$(TIME$,4,2)))-VAL(RIGHT$(TIME$,2))
4030 DUMMY= RND(SE)
4040 DUMMY= RND(1)
4060 FOR N%=0 TO MN%-1 ' while loop
4065 I2%=0 : I%=FIX(RND(1)*(256-J%)):J%=J%+1
4066 TS%(15)=(VARPTR(TS%(0))+8):TS%(1)=VARPTR(I2%):CALL VARPTR(TS%(0)),I%,VARPTR(MA%(0))
4070 TX%=PEEK(VARPTR(MA%(I2%))) : TY%=PEEK(VARPTR(MA%(I2%))+1)
4110 S%(TX%,TY%,0)=1 : MI%(N%,0)=TX% : MI%(N%,1)=TY% : MA%(I2%)=-1 : NEXT
4200 M=1 'minefield generation complete
4400 PRINT @ 129,"CALCULATING...";
4600 FOR N%=0 TO MN%-1 'generate mine adjecency counts
4610 CX = MI%(N%,0): CY = MI%(N%,1)
4630 POKE VARPTR(XY%(0)), CX: POKE VARPTR(XY%(0))+1, CY : CALL VARPTR(XY%(3)),0,VARPTR(XY%(0)) 
4640 FOR TY% = PEEK(VARPTR(XY%(2))) TO PEEK(VARPTR(XY%(2))+1):FOR TX% = PEEK(VARPTR(XY%(1))) TO PEEK(VARPTR(XY%(1))+1)
4650 S%(TX%,TY%,2)=S%(TX%,TY%,2)+1
4660 NEXT:NEXT:NEXT
4700 PRINT @ 129, STRING$(14,239)
5000 IF S%(X,Y,0)=2 AND S%(X,Y,2)>0 THEN GOTO 17000
5010 IF S%(X,Y,1)<>1 AND S%(X,Y,0)<>2 THEN ST%(W%,0)=X:ST%(W%,1)=Y:W%=W%+1:S%(X,Y,1)=2 ELSE GOTO 3000'reveal board if we hit a mine 
5060 CX=ST%(R%,0):CY=ST%(R%,1):R%=R%+1 ' grab the top cell from the stack
5070 IF S%(CX,CY,0)=1 OR S%(CX,CY,0)=3 THEN GOTO 12000 ELSE MC%=MC%-1 : S%(CX,CY,0)=2 ' if it's a mine, end sequence, else reveal
6010 C=S%(CX,CY,2)
7160 IF C=0 THEN PRINT @ (CY*40)+CX,CHR$(32);:S%(CX,CY,1)=32:GOTO 8010
7170 IF C>0 THEN PRINT @ (CY*40)+CX,RIGHT$(STR$(C),1);:S%(CX,CY,1)=ASC(RIGHT$(STR$(C),1))
7300 IF R%=W% THEN GOTO 3200
7400 IF R%<W% THEN GOTO 5060
8010 POKE VARPTR(XY%(0)), CX:POKE VARPTR(XY%(0))+1, CY
8020 CALL VARPTR(XY%(3)), 0, VARPTR(XY%(0)) 
8050 FOR TY% = PEEK(VARPTR(XY%(2))) TO PEEK(VARPTR(XY%(2))+1)
8060 FOR TX% = PEEK(VARPTR(XY%(1))) TO PEEK(VARPTR(XY%(1))+1)
8070 IF S%(TX%,TY%,0)<2 THEN ST%(W%,0)=TX%:ST%(W%,1)=TY%:W%=W%+1:S%(TX%,TY%,0)=S%(TX%,TY%,0)+2
8090 NEXT TX% : NEXT TY%
8160 IF R%<W% GOTO 5060 ELSE GOTO 3200
12000 FOR C = 0 TO MN%-1 : PRINT @ MI%(C,0)+MI%(C,1)*40, CHR$(42); : NEXT C 'reveal all mines
12200 FOR N=0 TO 6:PRINT @ (N*40)+32, ME$(N);:NEXT N ' game end message
12270 KEY(8)STOP:GOSUB 20000: IF YN%=1 THEN GOTO 1 ELSE CLS:MENU
14000 CLS : PRINT CHR$(27)+CHR$(85);
14030 PRINT @ 94,"TEXT SWEEPER":PRINT @ 133,"BY G.M. RIMAKIS":PRINT @ 207,"Press [SPACEBAR] to start."
14040 PRINT @ 7*40, "Ver. "+VE$;
14060 S$="Q"+CHR$(32)
14070 ON INSTR(1,S$,INKEY$) GOTO 14070,14100
14080 GOTO 14070
14100 CLS:S$="Q"+CHR$(28)+CHR$(29)+CHR$(31)+CHR$(30)+CHR$(70)+CHR$(102)+CHR$(32)+CHR$(13)+"WwAaSsDdHh":CN$=CHR$(27)+CHR$(80):CF$=CHR$(27)+CHR$(81)
14110 PRINT@127,"Use default settings?(y/n)":GOSUB 20000:IF YN%=0 THEN GOTO 14250
14200 MN%=36:GOTO 15100
14250 CLS:INPUT"Enter number of mines. (10-217)";MN%
14300 IF MN%<10 OR MN%>217 GOTO 14250
15100 M=0:MC%=256-MN%
16060 LET X=0:Y=0:N=0:M=0:CX=0:CY=0:XL=0:XU=0:YL=0:YU=0:FC%=0:YO=0:SEC=0:DUMMY=0:Q=0:TX%=0:TY%=0:DX=0:DY=0
16070 DIM S%(31,7,2) 'the state of the field, x, y, status/flag/mine_count
16075 DIM ST%(271,1) 'the stack, used in the search algo
16076 DIM MI%(MN%-1,1) 'index of mines, x,y
16077 DIM ME$(6)
16078 ME$(0)="     YOU":ME$(1)="   LOSE!":ME$(2)="        ":ME$(3)="    Play":ME$(4)="  Again?":ME$(5)="   (Y/N)":ME$(6)=ME$(2)
16080 CLS:GOSUB 17200:FOR Y = 0 TO 7
16085 PRINT @ Y*40,STRING$(32,239);
16150 NEXT Y
16180 X=16:Y=3
16200 ON KEY GOSUB ,,,,,,,23000
16300 IF LD%=1 THEN GOSUB 30000
16500 GOTO 2915
17000 POKE VARPTR(XY%(0)), X:POKE VARPTR(XY%(0))+1, Y : CALL VARPTR(XY%(3)), 0, VARPTR(XY%(0)) 
17020 FC%=0:W%=0:R%=0:FOR N% = 0 TO 1:FOR TY% = PEEK(VARPTR(XY%(2))) TO PEEK(VARPTR(XY%(2))+1):FOR TX% = PEEK(VARPTR(XY%(1))) TO PEEK(VARPTR(XY%(1))+1)
17030 IF N% = 0 AND S%(TX%,TY%,1)=1 THEN FC%=FC%+1
17035 IF N% = 1 AND S%(TX%,TY%,0)<2 AND S%(TX%,TY%,1)<>1 THEN ST%(W%,0)=TX%:ST%(W%,1)=TY%:W%=W%+1:S%(TX%,TY%,0)=S%(TX%,TY%,0)+2
17040 NEXT TX%: NEXT TY% : IF FC%=S%(X,Y,2) THEN NEXT N% ELSE GOTO 3000
17050 IF W%<>0 THEN GOTO 5060 ELSE GOTO 3000
17100 CLS : GOSUB 17200 : GOSUB 17300 : GOSUB 18000 : RETURN 'subroutine: redraw the entire screen
17200 PRINT @ 32,"    TEXT";:PRINT @ 72," SWEEPER";:RETURN
17300 PRINT @ 152," SQUARES";:PRINT @ 192,"   LEFT:";:PRINT @ 232,"        ";:PRINT @ 232,"    "+STR$(MC%);:PRINT @ 312,H$;:RETURN
18000 GOSUB 24000:KEY(8) STOP:FOR TY%=0 TO 7:LS$="":FOR TX%=0 TO 31 ' Redraw the minefield using S%
18010 IF S%(TX%,TY%,1)=1 THEN T$=CHR$(255) ELSE T$=CHR$(239)
18020 IF S%(TX%,TY%,0)>1 THEN T$=RIGHT$(STR$(S%(TX%,TY%,2)),1)
18030 IF T$="0" THEN T$=" "
18040 LS$ = LS$+T$:NEXT TX%:PRINT @ 40*TY%, LS$;:NEXT TY%:KEY(8) ON:RETURN
20000 GOSUB 24000:A$=INKEY$ ' generic yes/no subroutine, updates YN%
20010 IF A$="" THEN A$="Q"
20020 IF A$="Y" OR A$="y" THEN YN%=1:RETURN
20030 IF A$="N" OR A$="n" THEN YN%=0:RETURN
20040 GOTO 20000
21000 IF LD%<>1 THEN GOSUB 30000 ELSE GOTO 3000'draw help screen if not T200 or DVI
21010 IF PEEK(65343)<>238 THEN PRINT CHR$(27)+"U":GOSUB 30000
21015 A$ = INKEY$:  IF A$="H" OR A$="h" THEN GOTO 21020 ELSE GOTO 21010
21020 GOSUB 17100 : GOTO 3000
23000 GOSUB 24000:GOSUB 31000:KEY(8) STOP: PRINT@155,"Quit?";:PRINT@195,"(y/n)";:GOSUB20000:KEY(8) ON:IF YN%=1 THEN CLS:MENU ELSE GOSUB 17200 : GOSUB 17300 : RETURN
24000 PRINT CF$:PRINT CHR$(27)+CHR$(86): RETURN
30000 IF LD%<>1 THEN CLS
30010 GOSUB 24000 ' subroutine: draw the help screen 
30020 KEY(8) STOP:PRINT@15+PO%,"Controls";
30030 PRINT@40+PO%, CHR$(235)+STRING$(38,231)+CHR$(236);
30040 PRINT@80+PO%, CHR$(233)+"Cursor:      [W][A][S][D]/"+"["+CHR$(152)+"]["+CHR$(155)+"]["+CHR$(153)+"]["+CHR$(154)+"]"+CHR$(234);
30050 PRINT@120+PO%, CHR$(233)+"Check Tile:            [Space]/[Enter]"+CHR$(234);
30060 PRINT@160+PO%, CHR$(233)+"Set/Unset Flag:                    [F]"+CHR$(234);
30070 IF LD%<>1 THEN PRINT@200+PO%, CHR$(233)+"Help Menu / Return:                [H]"+CHR$(234); ELSE PRINT@200+PO%, CHR$(233); :PRINT@239+PO%, CHR$(234);
30080 PRINT@240+PO%, CHR$(233)+"Exit to Menu:                     [F8]"+CHR$(234);
30090 PRINT@280+PO%, CHR$(237)+STRING$(38,232)+CHR$(238);:KEY(8) ON:RETURN
31000 FOR TY = 0 TO 7 'subroutine: Clear Right Pane
31010 PRINT @ TY*40+32,STRING$(8,32);
31020 NEXT TY : RETURN